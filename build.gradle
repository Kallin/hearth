apply plugin: "project-report"
apply plugin: 'groovy'
apply plugin: "idea"
apply plugin: "jacoco"
apply plugin: 'codenarc'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.7'
    compile group: 'org.luaj', name: 'luaj-jse', version: '3.0-beta1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.11'
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDir 'src/main/java'


idea {
    project {
        ipr {
            //todo: see about getting this into gradle core
            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'

                def gradleSettings = getFreshIprComponent(provider.node, 'GradleSettings')
                gradleSettings.appendNode('option', [name: 'gradleHome', value: gradle.gradleHomeDir.absolutePath])

                def javacSettings = getFreshIprComponent(provider.node, 'JavacSettings')
                javacSettings.appendNode('option', [name: 'MAXIMUM_HEAP_SIZE', value: "256"])

                provider.node.component.find { it.@name == 'ProjectRootManager' }.@languageLevel = 'JDK_1_7'
            }
        }
    }
}

/**
 * Returns a brand new Ipr component node with no children. If an existing component node is found it is removed along
 * with all of its children and a fresh one is created.
 */
def getFreshIprComponent(Node projectNode, String componentName) {
    Node existingComponent = (Node) projectNode.component.find { it.@name == componentName }
    if (existingComponent) {
        projectNode.remove(existingComponent)
    }
    return projectNode.appendNode('component', [name: componentName])
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

codenarc.configFile = file 'config/codenarc/rules.groovy'

